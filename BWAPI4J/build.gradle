import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

group 'org.openbw.bwapi4j'
version '0.8'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.3"
    }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.11.0'
    compile 'org.apache.logging.log4j:log4j-core:2.11.0'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'com.github.xaguzman:pathfinding:0.2.6'
    compile 'net.lingala.zip4j:zip4j:1.3.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.commons:commons-compress:1.16.1'
}

shadowJar {
    relocate 'org.apache.commons', 'org.openbw.bwapi4j.org.apache.commons'
    relocate 'org.xguzm', 'org.openbw.bwapi4j.org.xguzm'
    relocate 'net.lingala.zip4j', 'org.openbw.bwapi4j.net.lingala.zip4j'
    classifier = null
    from ('../BWAPI4JBridge/Release') {
        include '*.dll'
        include '*.so'
    }
}

test {
    // stefanglase, Nov 27 2010, https://stackoverflow.com/a/4292739
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED
//                TestLogEvent.SKIPPED,
//                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

//        // set options for log level DEBUG and INFO
//        debug {
//            events TestLogEvent.STARTED,
//                    TestLogEvent.FAILED,
//                    TestLogEvent.PASSED,
//                    TestLogEvent.SKIPPED,
//                    TestLogEvent.STANDARD_ERROR,
//                    TestLogEvent.STANDARD_OUT
//            exceptionFormat TestExceptionFormat.FULL
//        }
//        info.events = debug.events
//        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

allprojects {
    // This is a temporary fix for ignoring incorrect javadocs formatting.
    // The javadoc formatting should probably be fixed properly.
    // For now, we'll disable the strict formatting enforcement flag (doclint).
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task generateJniHeaders(type:Exec) {
    def classpath = sourceSets.main.output.classesDir
    def nativeIncludes = "src/native/include"
    commandLine "javah", "-jni", "-verbose", "-d", nativeIncludes, "-classpath", classpath,
            "org.openbw.bwapi4j.BW",
            "org.openbw.bwapi4j.BWMapImpl",
            "org.openbw.bwapi4j.InteractionHandler",
            "org.openbw.bwapi4j.MapDrawer",
            "org.openbw.bwapi4j.DamageEvaluator",
            "org.openbw.bwapi4j.unit.Unit",
            "net.lingala.zip4j.core.ZipFile" //javah complains if this is not specified

    dependsOn classes
}

class Constants {
    private static String BUILD_PREFIX = "build";
    private static String FILENAME_WORD_SEPARATOR = "_";

    private class Utils {
        static String concatenateWith(final String separator, final String firstString, final String... remainingStrings) {
            if (firstString == null) {
                throw new IllegalArgumentException();
            }
            String result = firstString;
            if (remainingStrings != null) {
                for (final String remainingString : remainingStrings) {
                    result += separator + remainingString;
                }
            }
            return result;
        }
    }

    enum Directory {
        Bridge("../BWAPI4JBridge");

        private final String path;

        private Directory(final String path) {
            this.path = path;
        }

        @Override
        String toString() {
            return this.path;
        }
    }

    enum Project { bridge, bwapi }

    enum BridgeType { vanilla, openbw }

    enum OS { windows, linux }

    enum ScriptType {
        WindowsBatch("bat"),
        LinuxBash("sh");

        private final String fileExtension;

        private ScriptType(final String fileExtension) {
            this.fileExtension = fileExtension;
        }

        String getFileExtension() {
            return this.fileExtension;
        }

        String appendFileExtension(final String path) {
            return (path + "." + this.fileExtension);
        }
    }

    enum Script {
        VANILLA_WINDOWS(BridgeType.vanilla, OS.windows, ScriptType.WindowsBatch),
        OPENBW_WINDOWS(BridgeType.openbw, OS.windows, ScriptType.WindowsBatch);

        private final BridgeType bridgeType;
        private final OS os;
        private final ScriptType scriptType;
        
        private Script(final BridgeType bridgeType, final OS os, final ScriptType scriptType) {
            this.bridgeType = bridgeType;
            this.os = os;
            this.scriptType = scriptType;
        }

        private String getFilename(final String project) {
            return scriptType.appendFileExtension(Constants.Utils.concatenateWith(Constants.FILENAME_WORD_SEPARATOR, Constants.BUILD_PREFIX, project, bridgeType.toString(), os.toString()));
        }

        String getBuildBridgeFilename() {
            return getFilename(Constants.Project.bridge.toString());
        }

        String getBuildBwapiFilename() {
            return getFilename(Constants.Project.bwapi.toString());
        }
    }
}

def bridgeDir = Constants.Directory.Bridge.toString();

task buildVanillaBwapiForWindows(type:Exec) {
    workingDir "$bridgeDir"
    commandLine 'cmd', '/c', Constants.Script.VANILLA_WINDOWS.getBuildBwapiFilename();
}

task buildVanillaBridgeForWindows(type:Exec) {
    workingDir "$bridgeDir"
    commandLine 'cmd', '/c', Constants.Script.VANILLA_WINDOWS.getBuildBridgeFilename();
    dependsOn generateJniHeaders, buildVanillaBwapiForWindows
}

task buildOpenBWBwapiForWindows(type:Exec) {
    workingDir "$bridgeDir"
    commandLine 'cmd', '/c', Constants.Script.OPENBW_WINDOWS.getBuildBwapiFilename();
}

task buildOpenBWBridgeForWindows(type:Exec) {
    workingDir "$bridgeDir"
    commandLine 'cmd', '/c', Constants.Script.OPENBW_WINDOWS.getBuildBridgeFilename();
    dependsOn generateJniHeaders, buildOpenBWBwapiForWindows
}
